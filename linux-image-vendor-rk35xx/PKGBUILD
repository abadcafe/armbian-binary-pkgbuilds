pkgname=linux-image-vendor-rk35xx
deburl="https://apt.armbian.com/pool/main/l/linux-6.1.115/linux-image-vendor-rk35xx_25.8.2_arm64__6.1.115-Sbd03-Deeea-P09c0-C3b8bH7b8f-HK01ba-Vc222-B14f4-R448a.deb"
_pkgver="$(echo "$deburl" | grep -Po '[^/]+.deb$' | cut -f 2 -d'_')"
pkgver="${_pkgver/-/_}"
_kver="$(echo "$deburl" | grep -Po '(?<=/linux-)[^/]+?(?=/)')"
_kverl="${_kver}${pkgname#linux-image}"
pkgrel=1
pkgdesc="Armbian Linux vendor kernel image $_kverl"
arch=('aarch64')
url="https://www.armbian.com/"
license=('GPL2' 'custom')
depends=('coreutils' 'kmod' 'initramfs')
optdepends=(
  'armbian-firmware: firmware images needed for some devices'
  'wireless-regdb: to set the correct wireless channels of your country'
)
source=("$pkgname-$pkgver.deb::$deburl")
sha256sums=('6151c34dc5e5c0166e9baa5181cd90181241769eba293e08aec823dbcd385d1e')
options=('!debug' '!strip')

package() {
  cd "$pkgdir"
  tar xvpf "$srcdir"/data.tar.*

  rm -rf etc
  mv "usr/lib/linux-image-${_kverl}" "boot/dtbs-${_kverl}"
  mv lib/* usr/lib/
  rm -rf lib

  # this action triggers mkinitcpio to build initramfs and copy vmlinuz.
  mv "boot/vmlinuz-${_kverl}" "usr/lib/modules/${_kverl}/vmlinuz"
  # mkinitcpio uses content of pkgbase to name vmlinuz, initramfs and
  # generate preset in /etc/mkinitcpio.d
  echo "${_kverl}" > "usr/lib/modules/${_kverl}/pkgbase"
}
