pkgname=linux-image-edge-rockchip64
deburl="https://apt.armbian.com/pool/main/l/linux-6.18.0-rc5/linux-image-edge-rockchip64_25.8.2_arm64__6.18-rc5-Se9a6-Deeea-Pc12b-C8ef0Hfdd9-HK01ba-Vc222-B2135-R448a.deb"
imgver="$(echo "$deburl" | grep -Po '[^/]+.deb$' | cut -f 2 -d'_')"
kver="$(echo "$deburl" | grep -Po '(?<=/linux-)[^/]+?(?=/)')"
kverl="${kver}${pkgname#linux-image}"
pkgver="${kver/-/.}_armbian_${imgver/-/.}"
pkgrel=1
pkgdesc="Armbian Linux edge kernel image $kverl"
arch=('aarch64')
url="https://www.armbian.com/"
license=('GPL2' 'custom')
depends=('coreutils' 'kmod' 'initramfs')
optdepends=(
  'armbian-firmware: firmware images needed for some devices'
  'wireless-regdb: to set the correct wireless channels of your country'
)
source=("$pkgname-$pkgver.deb::$deburl")
sha256sums=('0289ef5ce312b2570b68c38036af4c72cbb4c6f9658e150fd830405050723fa6')
options=('!debug' '!strip')

package() {
  cd "$pkgdir"
  tar xvpf "$srcdir"/data.tar.*

  rm -rf etc
  mv "usr/lib/linux-image-${kverl}" "boot/dtbs-${kverl}"
  mv lib/* usr/lib/
  rm -rf lib

  # this action triggers mkinitcpio to build initramfs and copy vmlinuz.
  mv "boot/vmlinuz-${kverl}" "usr/lib/modules/${kverl}/vmlinuz"
  # mkinitcpio uses content of pkgbase to name vmlinuz, initramfs and
  # generate preset in /etc/mkinitcpio.d
  echo "${kverl}" > "usr/lib/modules/${kverl}/pkgbase"
}
