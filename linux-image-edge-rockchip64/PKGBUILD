pkgname=linux-image-edge-rockchip64
deburl="https://mirrors.bfsu.edu.cn/armbian/pool/main/l/linux-6.18.0-rc3/linux-image-edge-rockchip64_25.8.2_arm64__6.18-rc3-Sdcb6-Deeea-Pc8ca-Cb40bH3e0d-HK01ba-Vc222-B14f4-R448a.deb"
_pkgver="$(echo "$deburl" | grep -Po '[^/]+.deb$' | cut -f 2 -d'_')"
pkgver="${_pkgver/-/_}"
_kver="$(echo "$deburl" | grep -Po '(?<=/linux-)[^/]+?(?=/)')"
_kverl="${_kver}${pkgname#linux-image}"
pkgrel=1
pkgdesc="Armbian Linux edge kernel image $_kverl"
arch=('aarch64')
url="https://www.armbian.com/"
license=('GPL2' 'custom')
depends=('coreutils' 'kmod' 'initramfs')
optdepends=(
  'armbian-firmware: firmware images needed for some devices'
  'wireless-regdb: to set the correct wireless channels of your country'
)
source=("$pkgname-$pkgver.deb::$deburl")
sha256sums=('8d236e36e181f070ad8a6095ed3009807946fd89dee61c06decf9ae8b7663299')
options=('!debug' '!strip')

package() {
  cd "$pkgdir"
  tar xvpf "$srcdir"/data.tar.*

  rm -rf etc
  mv "usr/lib/linux-image-${_kverl}" "boot/dtbs-${_kverl}"
  mv lib/* usr/lib/
  rm -rf lib

  # this action triggers mkinitcpio to build initramfs and copy vmlinuz.
  mv "boot/vmlinuz-${_kverl}" "usr/lib/modules/${_kverl}/vmlinuz"
  # mkinitcpio uses content of pkgbase to name vmlinuz, initramfs and
  # generate preset in /etc/mkinitcpio.d
  echo "${_kverl}" > "usr/lib/modules/${_kverl}/pkgbase"
}
