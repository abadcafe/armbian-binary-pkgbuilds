pkgname=armbian-firmware
deburl="https://apt.armbian.com/pool/main/a/armbian-firmware/armbian-firmware_25.8.2_all__1-SA5d4d-B6c7f-R448a.deb"
debname="$(grep -Po '[^/]+\.deb$' <<< "$deburl")"
debver="$(cut -f2 -d'_' <<< "$debname")"
pkgver="${debver/-/_}"
pkgrel=1
pkgdesc="Armbian - Linux firmware"
arch=('any')
url="https://www.armbian.com/"
license=('GPL2' 'custom')
provides=('linux-firmware' 'firmware-ap6611s')
source=("$debname::$deburl")
sha256sums=('0dccf4cf9f3b5d7554bb5471acb55f1fbe041abd0346fa332e96060b796f8592')
options=('!debug' '!strip')

package() {
  cd "$pkgdir"
  tar xvpf "$srcdir/"data.tar.*

  find . -type f -exec chmod 644 {} \;
  find . -type d -exec chmod 755 {} \;
  mv lib usr/

  # fix symlinks for ap6611s's firmware.
  cd "usr/lib/firmware/brcm"
  ln -s SYN43711A0.hcd "BCM.xunlong,orangepi-5-max.hcd"
  ln -s SYN43711A0.hcd "BCM.xunlong,orangepi-5-ultra.hcd"
}
