pkgname=armbian-firmware
deburl="https://apt.armbian.com/pool/main/a/armbian-firmware/armbian-firmware_25.8.2_all__1-SAd39a-B6c7f-R448a.deb"
_pkgver="$(echo "$deburl" | grep -Po '[^/]+$' | cut -f 2 -d'_')"
pkgver="${_pkgver/-/_}"
pkgrel=1
pkgdesc="Armbian - Linux firmware"
arch=('any')
url="https://www.armbian.com/"
license=('GPL2' 'custom')
provides=('linux-firmware' 'firmware-ap6611s')
source=("$pkgname-$pkgver.deb::$deburl")
sha256sums=('815d9384904651e3fe54f070d1801cd08a258a7643a02d6723f4fdcb090fed71')
options=('!debug' '!strip')

package() {
  cd "$pkgdir"
  tar xvpf "$srcdir/"data.tar.*

  find . -type f -exec chmod 644 {} \;
  find . -type d -exec chmod 755 {} \;
  mv lib usr/

  # fix symlinks for ap6611s's firmware.
  cd "usr/lib/firmware/brcm"
  ln -s SYN43711A0.hcd "BCM.xunlong,orangepi-5-max.hcd"
  ln -s SYN43711A0.hcd "BCM.xunlong,orangepi-5-ultra.hcd"
}
